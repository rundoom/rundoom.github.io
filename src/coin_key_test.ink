INCLUDE ink_prekoles.ink
~ money_name = "Золота" //Название игровой валюты в родительном падеже
~ money_name = ""//Если наличие денег не подразумевается, делаем пустой строкой
LIST mysterious_room_content = (Ключ), (Монета)// Тут объявляем отдельный список предметов для каждой локации или существа у которых есть предметы.
LIST start_items = Стартовый_предмет_1, Стартовый_предмет_2//Тут объявляем те предметы которые не привязаны к локациям или существам - например стартовые предметы  
~ inventory += start_items
-> move(-> mysterious_room)//Переходы делаем через move, а не просто -> knot

=== mysterious_room ===//Knot'ами мы обозначаем локации
~ current_location = "Таинственная комната"
Вы находитесь в таинственной комнате. 
В углу есть запертая дверь. {iterate("На полу есть", mysterious_room_content)}. 
* [Взять ключ] 
{to_inventory(mysterious_room_content, Ключ)}
  Вы взяли ключ.
  -> move(-> mysterious_room)
* [Взять монету] 
  {to_inventory(mysterious_room_content, Монета)}//Отнимаем Монету у локации и передаём в инвентарь
  Вы взяли монету.
  -> move(-> mysterious_room)
* {inventory has Ключ} [Использовать ключ на двери] -> move(-> mysterious_room.use_key)
* [Выбить дверь ногой] -> //kick_the_door(Переходы на несуществующие ветки необходимо комментить как в этом примере)
+ [Пойти в коридор] -> move(-> corridor)
+ [Выбить дверь головой] 
~ hp -= 5
Это было больно и неэффективно
-> move(-> mysterious_room)

= use_key
~ inventory -= Ключ
Вы использовали ключ. Дверь открылась и исчезла, открыв проход в сад.
+ [Пойти в сад] -> move(-> garden)
+ [Остаться в комнате] -> move(-> mysterious_room)

=== corridor ===
~ current_location = "Коридор"
Длинный темный коридор. Здесь довольно пусто.
+ [Вернуться в таинственную комнату] -> move(-> mysterious_room)
+ {inventory has Монета} [Подбросить монету]
Вы подбросили монету. {~Выпал орёл|Выпала решка}!
-> move(-> corridor)
+ [Пойти дальше по коридору] -> move(-> corridor.corridor_end)

= corridor_end
Коридор заканчивается тупиком.
+ [Вернуться назад] -> move(-> corridor)

=== garden ===
~ current_location = "Сад"
Красивый сад с цветами и деревьями. Здесь очень мирно.
+ [Вернуться в таинственную комнату] -> move(-> mysterious_room)
* [Закончить игру] -> move(-> end_game)